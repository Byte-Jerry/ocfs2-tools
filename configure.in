AC_PREREQ(2.54)

AC_INIT(libocfs2/bitmap.c)

PACKAGE=ocfs2-tools

AC_SUBST(PACKAGE)

# Adjust these for the software version.
MAJOR_VERSION=0
MINOR_VERSION=99
MICRO_VERSION=0
EXTRA_VERSION=BETA1

# Adjust this only to bump the RPM packaging version
RPM_VERSION=1

DIST_VERSION=$MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION
VERSION=$DIST_VERSION-$EXTRA_VERSION

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(MICRO_VERSION)
AC_SUBST(EXTRA_VERSION)

AC_SUBST(DIST_VERSION)
AC_SUBST(VERSION)
AC_SUBST(RPM_VERSION)

AC_CANONICAL_HOST

OCFS_PROCESSOR=

case "$host" in
  *-*-linux*)
    ;;
  *)
    AC_MSG_ERROR([This filesystem will only work on Linux])
    ;;
esac

case "$host_cpu" in
  powerpc64)
    OCFS_PROCESSOR="ppc64"
    ;;
  ia64)
    OCFS_PROCESSOR="ia64"
    ;;
  x86_64)
    OCFS_PROCESSOR="x86_64"
    ;;
  i386|i486|i586|i686|i786|k6|k7)
    OCFS_PROCESSOR="i686"
    ;;
  s390x)
    OCFS_PROCESSOR="s390x"
    ;;
  *)
    AC_MSG_ERROR([not configured for "$host_cpu"])
    ;;
esac

AC_SUBST(OCFS_PROCESSOR)

if test -z "$TOOLSARCH"; then
    case "$host_cpu" in
    x86_64|i386|ppc|ia64|s390x)
        TOOLSARCH="$host_cpu"
        ;;
    i486|i586|i686|i786|k6|k7)
        TOOLSARCH="i386"
        ;;
    powerpc64)
        TOOLSARCH="ppc"
        ;;
    *)
        AC_MSG_RESULT([not found])
        AC_MSG_ERROR([invalid or unsupported CPU type])
        ;;
    esac
fi

AC_SUBST(TOOLSARCH)

AC_PROG_CC
AC_PROG_CPP

AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

AC_PATH_PROG(AR, ar)

AC_HEADER_STDC
AC_C_CONST

if test "x$GCC" != "xyes"; then
  AC_MSG_ERROR(GCC is required)
fi

AC_MSG_CHECKING(for debugging)
AC_ARG_ENABLE(debug, [  --enable-debug=[yes/no]         Turn on debugging [default=no]],,enable_debug=no)
OCFS_DEBUG=
if test "x$enable_debug" = "xyes"; then
  OCFS_DEBUG=yes
fi
AC_SUBST(OCFS_DEBUG)
AC_MSG_RESULT($enable_debug)

COM_ERR_LIBS=
AC_CHECK_LIB(com_err, com_err, COM_ERR_LIBS=-lcom_err)
if test "x$COM_ERR_LIBS" = "x"; then
    AC_MSG_ERROR([Unable to find com_err library])
fi
AC_SUBST(COM_ERR_LIBS)

AC_C_BIGENDIAN()

AC_MSG_CHECKING(for debug executables)
AC_ARG_ENABLE(debugexe, [  --enable-debugexe=[yes/no]     Enable debug executables for library source files [default=no]],,enable_debugexe=no)
OCFS2_DEBUG_EXE=
if test "x$enable_debugexe" = "xyes"; then
    OCFS2_DEBUG_EXE=yes
fi
AC_SUBST(OCFS2_DEBUG_EXE)
AC_MSG_RESULT($enable_debugexe)

GLIB_REQUIRED_VERSION=2.0.6
GTK_REQUIRED_VERSION=2.2.4
PYGTK_REQUIRED_VERSION=1.99.16

AM_PATH_GLIB_2_0($GLIB_REQUIRED_VERSION, have_glib=yes, have_glib=no)

if test "x$have_glib" != "xyes"; then
  AC_MSG_WARN([Glib $GLIB_REQUIRED_VERSION or better not found, some tools won't be built])
fi

BUILD_OCFS2CDSL=
BUILD_DEBUGOCFS2=

if test "x$have_glib" = "xyes"; then
  BUILD_OCFS2CDSL=yes
  
  ocfs_tools_save_LIBS=$LIBS
  LIBS="$LIBS -lncurses"
  AC_CHECK_LIB(readline, readline,
    [AC_CHECK_HEADER(readline/readline.h,
      BUILD_DEBUGOCFS2=yes,
      [AC_MSG_WARN([readline not found, debugfs.ocfs2 will not be built])])],
    [AC_MSG_WARN([readline not found, debugfs.ocfs2 will not be built])])
  LIBS=$ocfs_tools_save_LIBS
fi

AC_SUBST(BUILD_OCFS2CDSL)
AC_SUBST(BUILD_DEBUGOCFS2)

BUILD_OCFS2TOOL=
dnl AC_ARG_ENABLE(ocfs2tool, [  --enable-ocfs2tool=[yes/no]       Build GUI frontend [default=yes]],,enable_ocfs2tool=yes)
AC_ARG_ENABLE(ocfs2tool, [  --enable-ocfs2tool=[yes/no]       Build GUI frontend [default=no]],,enable_ocfs2tool=no)

if test "x$have_glib" != "xyes"; then
  enable_ocfs2tool=no
fi

if test "x$enable_ocfs2tool" = "xyes"; then
  dnl check for Python
  AM_PATH_PYTHON(2.2)
  AM_CHECK_PYTHON_HEADERS(,[AC_MSG_ERROR(could not find Python headers)])

  dnl check for GTK
  PKG_CHECK_MODULES(GTK, gtk+-2.0 >= $GTK_REQUIRED_VERSION)

  dnl check for PyGTK
  PKG_CHECK_MODULES(PYGTK, pygtk-2.0 >= $PYGTK_REQUIRED_VERSION)
  AC_SUBST(PYGTK_CFLAGS)

  AC_PATH_PROG(PYGTK_CODEGEN, pygtk-codegen-2.0, no)
  if test "x$PYGTK_CODEGEN" = xno; then
    AC_MSG_ERROR(could not find pygtk-codegen-2.0 script)
  fi

  AC_MSG_CHECKING(for pygtk defs)
  PYGTK_DEFSDIR=`$PKG_CONFIG --variable=defsdir pygtk-2.0`
  AC_SUBST(PYGTK_DEFSDIR)
  AC_MSG_RESULT($PYGTK_DEFSDIR)

  BUILD_OCFS2TOOL=yes
fi

AC_SUBST(BUILD_OCFS2TOOL)

AC_OUTPUT([
Config.make
libocfs2/ocfs2_err.et
libo2cb/o2cb_err.et
debugfs.ocfs2/debugfs.ocfs2.8
mkfs.ocfs2/mkfs.ocfs2.8
fsck.ocfs2/fsck.ocfs2.8
fsck.ocfs2/fsck.ocfs2.checks.8
ocfs2tool/ocfs2tool.8
vendor/common/ocfs2-tools.spec
])
